apply plugin: 'java'


import jsettlers.textures.generation.TextureGenerationTask
import org.eclipse.jgit.lib.Repository
import org.eclipse.jgit.storage.file.FileRepositoryBuilder


def generatedDirectory = 'src/generated/'
def generatedCommitInfoDirectory = generatedDirectory + 'commitInfo/'
def generatedTexturesDirectory = generatedDirectory + 'textures/'


task compileTextures(type: TextureGenerationTask) {
    resourceDirectory = file('raw_images_for_textures')
    generatedSourcesDirectory = file(generatedTexturesDirectory + 'java')
    generatedResourcesDirectory = file(generatedTexturesDirectory + 'resources')
}

task findGitRevision {
    def commitFile = file(generatedCommitInfoDirectory + 'java/jsettlers/common/CommitInfo.java')

    FileRepositoryBuilder builder = new FileRepositoryBuilder()
    Repository repository = builder.readEnvironment().findGitDir(file('.')).build()
    def headRef = repository.getRef("HEAD")
    def localRev = headRef.objectId.name
    repository.close()

    onlyIf {
        !commitFile.isFile() || !commitFile.text.contains(localRev)
    }
    doLast {
        commitFile.parentFile.mkdirs()
        commitFile.text = sprintf("""package jsettlers.common;
public final class CommitInfo {
	/**
	 * The current commit this was build with, as extracted from git.
	 */
	public static final String COMMIT_HASH_SHORT = "%s";
	public static final String COMMIT_HASH = "%s";
	public static final String BUILD_TIME = "%s";
	private CommitInfo() {
	}
}""", localRev.substring(0, 8), localRev, (new Date()).format('yyyy-MM-dd-HH-mm-ss'))
    }
}



sourceSets {
    main.java.srcDirs = [generatedTexturesDirectory + 'java', generatedCommitInfoDirectory + 'java', "src/main/java", 'src/gen/java']
    main.resources.srcDirs = [generatedTexturesDirectory + 'resources', 'src/main/resources']
    main.output.dir('src/gen/java', buildBy: 'generateLayouts')
}

compileJava {
    dependsOn compileTextures
    dependsOn findGitRevision
    dependsOn 'generateLayouts'
}

clean {
    delete generatedDirectory
    delete 'src/gen'
}

dependencies {
    compile project(':go.graphics')
    implementation 'com.google.code.gson:gson:2.8.2'
    testImplementation project(':jsettlers.testutils')
}


def layoutsFolder = './src/gen/java/jsettlers/graphics/ui/layout'

task generateLayouts(dependsOn: tasks.getByPath(':layoutbuilder:jar'), type: JavaExec) {
    doFirst {
        delete(layoutsFolder)
        mkdir(layoutsFolder)
    }

    classpath project(":layoutbuilder").sourceSets.main.runtimeClasspath
    main = "jsettlers.graphics.ui.generate.LayoutConverter"
    args = [file('./src/main/res/layout').absolutePath, file('./src/gen/java').absolutePath]
    inputs.dir file('./src/main/res/layout')
    outputs.dir file(layoutsFolder)
}